#include "supnorm.sollya";
#include "sos.sollya";


verbosity = 1!;

midpointmode = on!;

f = sin(x);
I = [-1/8;1/8];
deg = 15;

p = fpminimax(f, [|1,...,deg|], [|DD,DD,DD,DD,DD,D...|], I);

mode = relative;

quality = 2^-24;



"Starting supnorm";
timeSupnorm = time(res = supnorm(p,f,I,quality,mode));
norm = res[0];
sosInstances = res[1];

"Certified supnorm", norm;

if (mode == absolute) then errFunc = p - f else errFunc = p/f-1;

timeDirtySupnorm = time(dirtyNorm = dirtyinfnorm(errFunc,I));

"Dirty supnorm     ", dirtyNorm;

"Time for supnorm:       ", ceil(timeSupnorm * 1000), "ms";
"Time for dirty supnorm: ", ceil(timeDirtySupnorm * 1000), "ms";

"Checking SOS instances";
timeSOS = time(sosResult = sos(sosInstances));

"SOS instances checking result", sosResult;
"Time for SOS checking:  ", ceil(timeSOS * 1000), "ms";

